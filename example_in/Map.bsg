import List;
import MapEntry;
import IO;
import StringEx;
import StringBuilder;
import ToString;
import System;
import AnyEx;

class Map: ToString {
    var bucketList: List;
    var size: Int;

    init(): Void {
        bucketList = new List();
        size = 0;

        var i: Int = 0;
        while(i < 10) {
            bucketList.add(new List() as Any);
            i = i + 1;
        }
    }

    put(key: <K: Any>, value: <V: Any>): Void {
        if(size + 1 > ((bucketList.size * 3) / 4)) {
            rehash(bucketList.size * 2);
        }

        var entry: MapEntry = new MapEntry();
        entry.key = key;
        entry.value = value;
        if(putInBucketList(entry, bucketList)) {
            size = size + 1;
        }
    }

    putInBucketList(entry: MapEntry, bucketListParam: List): Bool {
        var bucket: List = getBucket(entry.key, bucketListParam);

        var i: Int = 0;
        while(i < bucket.size && AnyEx.equalityOf((bucket.get(i) as MapEntry).key, entry.key) == false) {
            i = i + 1;
        }

        if(i < bucket.size) {
            bucket.set(i, entry as Any);
            return false;
        }
        bucket.add(entry as Any);
        return true;
    }

    getBucket(key: Any, bucketListParam: List): List {
        var index: Int = AnyEx.hashOf(key) % bucketListParam.size;
        if(index < 0) {
            index = index + bucketListParam.size;
        }

        return bucketListParam.get(index) as List;
    }

    containsKey(key: <K: Any>): Bool {
        var bucket: List = getBucket(key, bucketList);

        var i: Int = 0;
        while(i < bucket.size && AnyEx.equalityOf((bucket.get(i) as MapEntry).key, key) == false) {
            i = i + 1;
        }

        return i < bucket.size;
    }

    get(key: <K: Any>): <V: Any> {
        var bucket: List = getBucket(key, bucketList);

        var i: Int = 0;
        while(i < bucket.size && AnyEx.equalityOf((bucket.get(i) as MapEntry).key, key) == false) {
            i = i + 1;
        }

        if(i < bucket.size) {
            return (bucket.get(i) as MapEntry).value;
        }
        System.error("Key does not exist in map: ".plus(StringEx.ofAny(key)));
        return 0 as Any;
    }

    remove(key: <K: Any>): Void {
        var bucket: List = getBucket(key, bucketList);

        var i: Int = 0;
        while(i < bucket.size && AnyEx.equalityOf((bucket.get(i) as MapEntry).key, key) == false) {
            i = i + 1;
        }

        if(i < bucket.size) {
            bucket.remove(i);
            size = size - 1;
        }
    }

    rehash(newCapacity: Int): Void {
        var newBucketList: List = new List();
        var i: Int = 0;
        while(i < newCapacity) {
            newBucketList.add(new List() as Any);
            i = i + 1;
        }

        i = 0;
        while(i < bucketList.size) {
            var bucket: List = bucketList.get(i) as List;
            var j: Int = 0;
            while(j < bucket.size) {
                var entry: MapEntry = bucket.get(j) as MapEntry;
                putInBucketList(entry, newBucketList);
                j = j + 1;
            }
            i = i + 1;
        }

        bucketList = newBucketList;
    }

    clear(): Void {
        init();
    }

    entries(): List<E: MapEntry<K: <K: Any>, V: <V: Any>>> {
        var list: List = new List();

        var i: Int = 0;
        while(i < bucketList.size) {
            var j: Int = 0;
            var bucket: List = bucketList.get(i) as List;
            while(j < bucket.size) {
                list.add(bucket.get(j));
                j = j + 1;
            }
            i = i + 1;
        }

        return list;
    }

    toString(): String {
        var builder: StringBuilder = new StringBuilder();
        builder.append("Map(");

        var entryList: List<E:MapEntry<K:<K:Any>,V:<V:Any>>> = entries();
        var i: Int = 0;
        while(i < entryList.size) {
            var entry: MapEntry<K:<K:Any>,V:<V:Any>> = entryList.get(i);
            if(i > 0) {
                builder.append(",");
            }
            builder.appendAny(entry.key);
            builder.append("=");
            builder.appendAny(entry.value);
            i = i + 1;
        }

        builder.append(")");

        return builder.toString();
    }
}